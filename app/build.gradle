plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.kotlin.ksp)
    alias(libs.plugins.android.junit5)
    alias(libs.plugins.compose.compiler)
}

android {
    namespace 'com.example.weatherapp'
    compileSdk libs.versions.compile.sdk.get().toInteger()

    defaultConfig {
        applicationId "com.example.weatherapp"
        minSdk libs.versions.min.sdk.get().toInteger()
        targetSdk libs.versions.target.sdk.get().toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["runnerBuilder"] = "de.mannodermaus.junit5.AndroidJUnit5Builder"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

kotlin {
    jvmToolchain(8)
}

dependencies {
    implementation project(':data')

    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.livedata
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.activity.compose

    // material
    implementation libs.androidx.compose.material
    implementation libs.androidx.compose.material3

    // Navigation
    implementation libs.androidx.navigation.compose

    // Compose
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.compose.animation
    implementation libs.androidx.compose.runtime.livedata

    // Moshi
    implementation libs.moshi
    implementation libs.moshi.adapters
    ksp libs.moshi.kotlin.codegen

    // DataStore
    implementation libs.androidx.datastore.preferences

    // Location services
    implementation libs.play.services.location

    // Dagger-Hilt
    implementation libs.dagger.hilt.android
    ksp libs.dagger.hilt.android.compiler

    // Hilt niceties
    implementation libs.hilt.navigation.compose
    ksp libs.hilt.compiler

    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.arch.core.testing
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.mockito.junit.jupiter
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.mockk

    androidTestImplementation libs.android.test.runner
    androidTestImplementation libs.android.test.ext.junit
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.android.test.rules
    androidTestImplementation libs.mockito.android
    androidTestImplementation libs.kotlinx.coroutines.test

    androidTestImplementation libs.androidx.compose.ui.test
    androidTestImplementation libs.androidx.compose.ui.test.junit4
    debugImplementation libs.androidx.compose.ui.tooling.debug
    debugImplementation libs.androidx.compose.ui.test.manifest

    androidTestImplementation libs.junit.jupiter
    androidTestImplementation libs.junit5.android.test.core
    androidTestRuntimeOnly libs.junit5.android.test.runner
    testRuntimeOnly libs.junit.jupiter.engine
}